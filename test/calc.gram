program: statements ENDMARKER { statements }

statements:
    | statement NL* statements { Block(List(statement) + statements.args) }
    | statement NL* { Block(List(statement)) }

statement:
    | 'if' test=expr ':' NEWLINE INDENT body=statements DEDENT 'else' ':' NEWLINE INDENT orelse=statements DEDENT { If(test, body, orelse) }
    | 'if' test=expr ':' NEWLINE INDENT body=statements DEDENT { If(test, body) }
    | 'while' test=expr ':' NEWLINE INDENT body=statements DEDENT { While(test, body) }
    | 'return' expr NEWLINE { Return(expr) }
    | expr NEWLINE { expr }

expr: assign

assign:
    | NAME '=' assign { Assign(name, assign) }
    | equality

equality:
    | equality '==' relational { Compare(equality, '==', relational) }
    | equality '!=' relational { Compare(equality, '!=', relational) }
    | relational

relational:
    | relational '<' add { Compare(relational, '<', add) }
    | relational '<=' add { Compare(relational, '<=', add) }
    | relational '>' add { Compare(relational, '>', add) }
    | relational '>=' add { Compare(relational, '>=', add) }
    | add

add:
    | add '+' term { BinOp(add, '+', term) }
    | add '-' term { BinOp(add, '-', term) }
    | term

term:
    | term '*' unary { BinOp(term, '*', unary) }
    | term '/' unary { BinOp(term, '/', unary) }
    | unary

unary:
    | '+' unary { UnaryOp('+', unary) }
    | '-' unary { UnaryOp('-', unary) }
    | atom

atom:
    | NAME
    | NUMBER
    | '(' expr ')' { expr }

